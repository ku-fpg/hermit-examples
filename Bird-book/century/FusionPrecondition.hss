-------------------------------------------------------------
--
-- Fusion Precondition for "Century"
--
--    \ x -> map (fork (id, value)) . extend' x
-- =>
--    \ x -> expand x . map (fork (id, value))

-- To test, apply this to the: rhs-of 'expandSpecProof

-------------------------------------------------------------

-- \ x -> map (fork (id, value)) . extend' x

{ lam-body

  -- map (fork (id, value)) . extend' x

       { one-td (unfold 'extend') ; unfold-rule "comp-assoc-R" }

  -- map (fork (id, value)) . filter (ok . value) . extend x

       { [ app-fun, app-arg ] ; unfold-rule "6.10" }
       { unfold-rule "comp-assoc-L" }

  -- filter (ok . snd) . map (fork (id,value)) . extend x

  { app-arg

    -- map (fork (id,value)) . extend x

          { [app-fun, app-arg]
            unfold-rule "6.9"
            one-td (unfold-rule "map-id")
          }
          { unfold-rule "comp-assoc-L" }

    -- zip . fork (id, map value) . extend x

    { app-arg

      -- fork (id, map value) . extend x

            { unfold-rule "6.6"
              one-td (unfold-rule "comp-id-L")
            }

      -- fork (extend x, (map value . extend x))

            { [app-arg, app-arg] ; unfold-rule "6.4" }

      -- fork (extend x, (modify x . map value))

            { app-arg ; arg 2 ; unfold-rule "comp-R-id" }

      -- fork (extend x . id, (modify x . map value))

            { unfold-rule "6.7" }

      -- cross (extend x, modify x) . fork (id, map value)

      { app-arg

        -- fork (id, map value)

              { one-td (unfold-rule "id-map") }

        -- fork (map id, map value)

              { unfold-rule "6.8" }

        -- unzip . map (fork (id,value))

      }

      -- cross (extend x, modify x) . unzip . map (fork (id,value))

    }

    -- zip . cross (extend x, modify x) . unzip . map (fork (id,value))

  }

  -- filter (ok . snd) . zip . cross (extend x, modify x) . unzip . map (fork (id,value))

  { unfold-rule "comp-hack"
    [ app-fun, app-arg ]

    -- filter (ok . snd) . zip . cross (extend x, modify x) . unzip

          { fold 'expand }

    -- expand x

  }

  -- expand x . map (fork (id, value))

}

-- \ x -> expand x . map (fork (id, value))