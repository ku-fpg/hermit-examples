-- hermit FoldrFusionExample.hs -opt=HERMIT.Dictionary.CenturyPlugin +FoldrFusionExample FoldrFusionExample.hec

set-auto-corelint True

{
  rule-to-lemma "Fusion Condition 1"
  rule-to-lemma "Fusion Condition 2"
  rule-to-lemma "Fusion Condition 3"
  rule-to-lemma "Foldr Fusion Example"

  prove-lemma "Fusion Condition 1"
  -- f undefined = undefined
  unfold 'f
  undefined-case
  end-proof

  prove-lemma "Fusion Condition 2"
  -- f a = b
  one-td (inline 'a)
  unfold 'f
  case-reduce
  fold 'b
  end-proof

  prove-lemma "Fusion Condition 3"
  -- f (g x y) = h x (f y)
  both (any-call (unfold [ 'h , 'g , 'f ]))
  lhs case-float-case
  one-td case-reduce
  end-proof

  prove-lemma "Foldr Fusion Example"
  -- f . foldr g a = foldr h b
  foldr-fusion-proof (forward (lemma "Fusion Condition 1")) (forward (lemma "Fusion Condition 2")) (forward (lemma "Fusion Condition 3"))
}

flatten-module
{
  rhs-of 'lr
  lemma "Foldr Fusion Example"
  lemma "Foldr Fusion Example"
}
{
  rhs-of 'rl
  lemma "Foldr Fusion Example"
  lemma "Foldr Fusion Example"
}
